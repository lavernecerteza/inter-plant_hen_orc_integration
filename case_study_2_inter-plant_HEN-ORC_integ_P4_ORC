Sets
    p       "Plants"                / P4, ORC           /
    s       "Intermediate fluid"    / hot_IF, cold_IF           / ;
    
Alias (p,pp) ;
Alias (s,ss) ;
    
Sets
    i                   "Hot process stream"    / 4_s10, 4_s33, 4_s60     /
    plant_IF(p,s,pp,ss) "Interconnections between plants and intermediate fluid"
                                                / P4.cold_IF.ORC.hot_IF,
                                                  ORC.hot_IF.P4.cold_IF  /
    plant_connect(p,pp) "Possible interconnections between plants"
                                                /   P4.ORC,
                                                    ORC.P4  / ;
    
Scalars
    Af                      "Annual factor for capital costs"                                           /   0.264       /
    Hy                      "Total operation time of one year in hr/year"                               /   8000        /
    cp                      "Intermediate fluid heat capacity flow rate in kJ/kg-K"                     /   2.2         /
    ho                      "Intermediate fluid heat transfer coefficient in kW/m^2-K"                  /   1.5         /
    Pe                      "Unitary buy-back price of produced electricity in $/kWh as of August 2024" /   0.08        /
    etta                    "Pump efficiency"                                                           /   0.7         /
    rho                     "Intermediate fluid density in kg/m^3"                                      /   862.2       /
    mu                      "Intermediate fluid viscosity in Pa-s"                                      /   0.00021667  /
    CU_cow                  "Cooling water utility cost in $1000/kW-yr"                                 /   0.01192     /
    CU_lp                   "Low-pressure steam  utility cost in $1000/kW-yr"                           /   0.14317     /
    CU_mp                   "Medium-pressure steam  utility cost in $1000/kW-yr"                        /   0.15043     /
    B_pipe                  "Piping cost parameter in $/m^1.7 units as of January 2006"                 /   4700        /
    n_pipe                  "Piping cost sizing exponent as of January 2006"                            /   1.7         /
    F                       "Cost of valves, fittings, and erection as a percentage of the pipe purchase cost"  /   1.5 /
    b_maint                 "Annual pipeline maintenance cost as a percentage of the pipe capital cost" /   0.05        /
    CEPCI_2001_plant        "Composite CEPCI as of May to December 2001"                                /   397         /
    CEPCI_2010_plant        "Composite CEPCI as of January 2010"                                        /   532.9       /
    CEPCI_2024_plant        "Composite CEPCI as of May 2024"                                            /   800.3       /
    CEPCI_2006_pipe         "CEPCI for pipes as of January 2006"                                        /   655.9       /
    CEPCI_2024_pipe         "CEPCI for pipes as of May 2024"                                            /   1357.1      /
    CEPCI_2010_HEX          "CEPCI for heat exchangers as of January 2010"                              /   571.9       /
    CEPCI_2024_HEX          "CEPCI for heat exchangers as of May 2024"                                  /   801.8       /
    CEPCI_2010_pump         "CEPCI for pumps as of January 2010"                                        /   903         /
    CEPCI_2024_pump         "CEPCI for pumps as of May 2024"                                            /   1543.2      /
    Tmin                    "Minimum temperature of intermediate fluid in deg C"                        /   40          /
    Tmax                    "Maximum temperature of intermediate fluid in deg C"                        /   390         /
    psi                     "Upper bound of the hot intermediate fluid stream heat capacity flow rate"  /   20          /
    epsilon                 "Pipeline insulation thickness in m"                                        /   0.127       /
    U                       "Overall insulated pipeline heat transfer coefficient in W/m^2-K"           /   0.326       /
    delta_T                 "Temperature difference between pipe wall and ambient air in deg C"         /   100         /
    delta                   "Small number added to the denominator to prevent division by zero"         /   0.000001    /
    C_power                 "Unitary purchase cost of electricity in US$/kWh as of August 2024"         /   0.282       /
    K1_pump                 "Pump capital cost parameter K1"                                            /   3.3892      /
    K2_pump                 "Pump capital cost parameter K2"                                            /   0.0536      /
    K3_pump                 "Pump capital cost parameter K3"                                            /   0.1538      /
    K1_HEX                  "HEX capital cost parameter K1"                                             /   4.3247      /
    K2_HEX                  "HEX capital cost parameter K2"                                             /   -0.303      /
    K3_HEX                  "HEX capital cost parameter K3"                                             /   0.1634      /
    K1_turb                 "Turbine capital cost parameter K1"                                         /   2.7051      /
    K2_turb                 "Turbine capital cost parameter K2"                                         /   1.4398      /
    K3_turb                 "Turbine capital cost parameter K3"                                         /   -0.1776     /
    
    Tmin_evap_ORC               "Minimum evaporator temperature in deg C"                                   /   70          /
    Tmax_cond_ORC               "Maximum condenser temperature in deg C"                                    /   60          /
    gamma_h_ORC_evap            "Upper bound of the temperature difference between the hot intermediate fluid and the ORC working fluid in the evaporator"      /   170     /
    gamma_coolant_ORC_cond      "Upper bound of the temperature difference between the condenser cooling water and the ORC working fluid in the condenser"      /   42.2    /
    TOUTmin_ORC_cond_coolant    "Minimum temperature of the cooling water at the condenser outlet in deg C" /   27.8        /
    Tmax_turb_outlet_ORC        "Maximum turbine outlet temperature in deg C"                               /   70          /
    EMAT_ORC                    "Exchanger minimum approach temperature in the ORC system in deg C"         /   10          /
    TIN_ORC_cond_coolant_1      "Temperature of the cooling water at the condenser inlet of ORC system 1 in deg C"          /   25          /
    h_ORC                       "ORC working fluid heat transfer coefficient in kW/m^2-K"                   /   1           /
    h_ORC_cond_coolant          "Heat transfer coefficient of the condenser coolant in kW/m^2-K"            /   1           /
    CU_orc_cond                 "Cooling water utility cost for the ORC condenser in $1000/kW-yr"           /   0.01192       /
    cp_cond_coolant             "Specific heat of the condenser cooling water in kJ/kg-K"                   /   4.187       / ;
    
Sets
    k_h       "Plant 4 temperature locations = NOK + 1"             /   1*4     /
    sth(k_h)  "Plant 4 superstructure stages"

    
Singleton Set
    firstKh(k_h)   "First temperature location in Plant 4"
    lastKh(k_h)    "Last temperature location in Plant 4" ;
    
sth(k_h)     = yes$(ord(k_h) < card(k_h));
firstKh(k_h) = yes$(ord(k_h) = 1);
lastKh(k_h)  = yes$(ord(k_h) = card(k_h));
  
Table tin_h(p,i)  "Supply temperature of hot process streams in deg C"
                4_s10   4_s33   4_s60
        P4      560     560     400  ;
        
Table tout_h(p,i) "Target temperature of hot process streams in deg C"
                4_s10   4_s33   4_s60
        P4      390     390     35  ;
        
Table h_h(p,i)    "Heat transfer coefficients of hot process streams in kW/m^2-K"
                4_s10   4_s33   4_s60 
        P4      1       1       1      ;
        
Table F_h(p,i)    "Hot process stream heat capacity flow rates in MW/K"
                4_s10   4_s33   4_s60 
        P4      0.0086  0.053   0.0086  ;
        
Table ech(p,i)  "Heat content of hot process streams in MW"
                4_s10   4_s33   4_s60 
        P4      1.468   9.018   3.126  ;
        
Table EMAT_h(p,i) "Minimum temperature difference between hot process stream and cold intermediate fluid in deg C"
                4_s10   4_s33   4_s60  
        P4      10      10      10    ;
        
Table L(p,pp)    "Distance between plants in m"
                P4          ORC
        P4                  1720.79        
        ORC     1720.79            ;
        
Parameter gamma_c(p,i)  "Upper bound of temperature difference between hot process stream and cold intermediate fluid"  ;
gamma_c(p,i)$(tin_h(p,i) - Tmin < EMAT_h(p,i)) = abs(tin_h(p,i) - Tmin) + EMAT_h(p,i) ;
gamma_c(p,i)$(tin_h(p,i) - Tmin >= EMAT_h(p,i)) = max(0, tin_h(p,i) - Tmin, Tmax - tout_h(p,i)) ;

    
Positive Variables
    y(p,s,pp,ss)            "Denotes existence of connection between two plants"
    zc(p,i,k_h)             "Denotes existence of heat exchanger between hot process stream and cold intermediate fluid" 
    m(p,s,pp,ss)            "Intermediate fluid stream heat capacity flow rate in MW/K"
    Mh(p)                     "Hot intermediate fluid stream heat capacity flow rate in MW/K"
    Mc(p)                     "Cold intermediate fluid stream heat capacity flow rate in MW/K"
    tcmin(p)                  "Inlet temperature of cold intermediate fluid in deg C"
    tcmout(p)                 "Outlet temperature of cold intermediate fluid in deg C"
    thmin(p)                  "Inlet temperature of hot intermediate fluid in deg C"
    thmout(p)                 "Outlet temperature of hot intermediate fluid in deg C"
    tcm(p,k_h)                "Cold intermediate fluid temperature in deg C"
    t_h(p,i,k_h)              "Hot process stream temperature in deg C"
    qc(p,i,k_h)               "Heat transferred between cold intermediate fluid and hot process stream in MW"
    qu_h(p,i)                 "Cold utility load in MW"
    dtc(p,i,k_h)              "Temperature difference between cold intermediate fluid and hot process stream in deg C"
    Din(p,s,pp,ss)              "Pipe inner diameter for intermediate fluid in m"
    Pl(p,s,pp,ss)               "Capital cost of pipeline per unit length in 1000$/m"
    Piping(p,s,pp,ss)           "Annualized piping cost for intermediate fluid in 1000$/yr"   
    v(p,s,pp,ss)                "Velocity of the intermediate fluid in m/s"
    Re(p,s,pp,ss)               "Reynolds number of the intermediate fluid in 1000 units"
    fr(p,s,pp,ss)               "Friction factor of the intermediate fluid"   
    delta_P(p,s,pp,ss)          "IHTF pressure drop along the pipeline in kPa"
    Q(p,s,pp,ss)                "Pump power consumption in kW"
    Pumpcapital(p,s,pp,ss)      "Capital cost of pump in 1000$"
    Pumpoperation(p,s,pp,ss)    "Annualized pump operation cost in 1000$/yr"
    Pumping(p,s,pp,ss)          "Annualized pumping cost for intermediate fluid in 1000$/yr"
    A_h(p,i,k_h)              "HEX area between cold intermediate fluid and hot process stream in m^2"
    HEX_h_cost(p,i,k_h)         "Capital cost of HEX between a hot process stream and cold IHTF at each superstructure stage in $1000"
    
    m_ORC_evap_1                "Heat capacity flow rate in MW/K of the hot intermediate fluid stream that exchanges heat with the ORC working fluid in the ORC evaporator of ORC system 1"
    m_ORC_bypass                "Heat capacity flow rate in MW/K of the hot intermediate fluid stream that bypasses the ORC evaporator"
    zorc_1                      "Denotes existence of ORC system 1 within the intermediate heat transfer fluid circuit"
    thminter_ORC_evap_1         "Temperature in deg C of the intermediate fluid at the intermediate point of the evaporator of ORC system 1"
    thmout_ORC_evap_1           "Temperature in deg C of the portion of the hot intermediate fluid stream that exchanges heat with the ORC working fluid as it exits the ORC evaporator of ORC system 1"
    tinter_ORC_cond_coolant_1   "Temperature in deg C of the condenser cooling water at the intermediate point of the condenser of ORC system 1"
    tout_ORC_cond_coolant_1     "Temperature in deg C of the condenser cooling water at the outlet of the condenser of ORC system 1"
    m_ORC_WF_1                  "Mass flow rate in kg/s of the ORC working fluid of ORC system 1"
    q_ORC_evap_1                "Total rate of heat transferred to the ORC working fluid in the evaporator of ORC system 1 in MW"
    q_ORC_evap_subcooled_1      "Rate of sensible heat transferred to the ORC working fluid in the evaporator of ORC system 1 in MW"
    q_ORC_evap_wet_1            "Rate of latent heat transferred to the ORC working fluid in the evaporator of ORC system 1 in MW"
    q_ORC_cond_1                "Total rate of heat rejected by the ORC working fluid in the condenser of ORC system 1 in MW"
    q_ORC_cond_superheat_1      "Rate of sensible heat rejected by the ORC working fluid in the condenser of ORC system 1 in MW"
    q_ORC_cond_wet_1            "Rate of latent heat rejected by the ORC working fluid in the condenser of ORC system 1 in MW"
    E_ORC_turb_1                "ORC turbine power production of ORC system 1 in MW"
    E_ORC_pump_1                "ORC pump power consumption of ORC system 1 in MW"
    Tevap                       "Evaporator temperature in deg C"
    Tcond                       "Condenser temperature in deg C"
    Pevap                       "Evaporator pressure in MPa"
    Pcond                       "Condenser pressure in MPa"
    T1_1                        "Temperature of the ORC working fluid at the condenser outlet of ORC system 1 in deg C"
    h1_1                        "Enthalpy of the ORC working fluid at the pump inlet of ORC system 1 in kJ/kg"
    s1_1                        "Entropy of the ORC working fluid at the pump inlet of ORC system 1 in kJ/kg-K"
    s2_1                        "Entropy of the ORC working fluid at the pump outlet of ORC system 1 in kJ/kg-K"
    T2_1                        "Temperature of the ORC working fluid at the evaporator inlet of ORC system 1 in deg C"
    h2_1                        "Enthalpy of the ORC working fluid at the evaporator inlet of ORC system 1 in kJ/kg"
    T3_1                        "Temperature of the ORC working fluid at the intermediate point of the evaporator of ORC system 1 in deg C"
    h3_1                        "Enthalpy of the ORC working fluid at the intermediate point of the evaporator of ORC system 1 in kJ/kg"
    T4_1                        "Temperature of the ORC working fluid at the turbine inlet of ORC system 1 in deg C"
    h4_1                        "Enthalpy of the ORC working fluid at the turbine inlet of ORC system 1 in kJ/kg"
    s4_1                        "Entropy of the ORC working fluid at the turbine inlet of ORC system 1 in kJ/kg-K"
    T5_1                        "Temperature of the ORC working fluid at the condenser inlet of ORC system 1 in deg C"
    h5_1                        "Enthalpy of the ORC working fluid at the condenser inlet of ORC system 1 in kJ/kg"
    s5_1                        "Entropy of the ORC working fluid at the condenser inlet of ORC system 1 in kJ/kg-K"
    T6_1                        "Temperature of the ORC working fluid at the intermediate point of the condenser of ORC system 1 in deg C"
    h6_1                        "Enthalpy of the ORC working fluid at the intermediate point of the condenser of ORC system 1 in kJ/kg"
    dt_in_ORC_evap_1            "Approach temperature at the evaporator inlet of ORC system 1 in deg C"
    dt_inter_ORC_evap_1         "Approach temperature at the intermediate point of the evaporator of ORC system 1 in deg C"
    dt_out_ORC_evap_1           "Approach temperature at the evaporator outlet of ORC system 1 in deg C"
    dt_in_ORC_cond_1            "Approach temperature at the condenser inlet of ORC system 1 in deg C"
    dt_inter_ORC_cond_1         "Approach temperature at the intermediate point of the condenser of ORC system 1 in deg C"
    dt_out_ORC_cond_1           "Approach temperature at the condenser outlet of ORC system 1 in deg C"
    A_ORC_evap_subcooled_1      "Evaporator area between the inlet and the intermediate point of ORC system 1 in m^2"
    A_ORC_evap_wet_1            "Evaporator area between the intermediate point and the outlet of ORC system 1 in m^2"
    A_ORC_cond_superheat_1      "Condenser area between the inlet and the intermediate point of ORC system 1 in m^2"
    A_ORC_cond_wet_1            "Condenser area between the intermediate point and the outlet of ORC system 1 in m^2"
    ORC_evap_subcooled_cost_1   "Capital cost of the evaporator area between the inlet and the intermediate point of ORC system 1 in $1000"
    ORC_evap_wet_cost_1         "Capital cost of the evaporator area between the intermediate point and the outlet of ORC system 1 in $1000"
    ORC_cond_superheat_cost_1   "Capital cost of the condenser area between the inlet and the intermediate point of ORC system 1 in $1000"
    ORC_cond_wet_cost_1         "Capital cost of the condenser area between the intermediate point and the outlet of ORC system 1 in $1000"
    Cost_ORC_pump_1             "Capital cost of the ORC pump of ORC system 1 in $1000"
    Cost_ORC_turb_1             "Capital cost of the ORC turbine of ORC system 1 in $1000"
    Revenue_ORC_1               "Annual revenue from the sale of produced electricity from ORC system 1 in $1000/yr"
    Cost_ORC_pump_power_cons_1  "Annual operational cost of the ORC pump of ORC system 1 in $1000/yr"
    m_ORC_cond_coolant_1        "Mass flow rate of the condenser cooling water of ORC system 1 in kg/s";

Variable
    TAC                     "Total annualized cost" ;
    
Equations
    Constraint_1c           "Mass balance for the hot intermediate fluid splitter of the ORC plant"
    Constraint_1d           "Mass balance at the internal splitter of the ORC plant"
    Constraint_2            "Mass balance for the cold intermediate fluid splitter of Plant 4"
    
    Constraint_3a           "Logical constraint for the connection between Plant 4 and ORC plant"
    Constraint_3d           "Logical constraint for the connection between ORC plant and Plant 4"
    
    Constraint_4c           "Mass balance for the hot intermediate fluid mixer of the ORC plant"
    Constraint_5            "Mass balance for the cold intermediate fluid mixer of Plant 4"
    
    Constraint_6a           "Heat loss from the pipeline connecting Plants 4 and the ORC plant"
    Constraint_7a           "Energy balance at the internal mixer of the ORC plant"
    Constraint_7b           "Heat loss from the pipeline connecting the ORC plant and Plant 4"
    
    Constraint_9            "Assignment of cold intermediate fluid outlet temperature at the start of the superstructure of Plant 4"    
    Constraint_10(i)           "Assignment of hot process stream supply temperature at the start of the superstructure of Plant 4"   
    Constraint_12           "Assignment of cold intermediate fluid inlet temperature at the end of the superstructure of Plant 4"   
    Constraint_14(i)        "Overall energy balance constraint for each hot process stream in Plant 4"  
    Constraint_16(i,k_h)      "Monotonic decrease of hot process stream temperature as stage number increases in Plant 4"  
    Constraint_19(k_h)        "Monotonic decrease of cold intermediate fluid temperature as stage number increases in Plant 4"
    Constraint_19a          "Monotonic decrease of hot intermediate fluid temperature at it passes through the inlet and intermediate point of the evaporator of ORC system 1"
    Constraint_19b          "Monotonic decrease of hot intermediate fluid temperature as it passes through the intermediate point and outlet of the evaporator of ORC system 1"
    Constraint_19c          "Monotonic increase of condenser cooling water temperature as it passes through the inlet and intermediate point of the condenser of ORC system 1"
    Constraint_19d          "Monotonic increase of condenser cooling water temperature as it passes through the intermediate point and outlet of the condenser of ORC system 1"

    Constraint_20c            "Heat released by hot intermediate fluid to the ORC working fluid" 
    Constraint_21(k_h)        "Heat absorbed by cold intermediate fluid from the hot process streams in Plant 4 at each stage"
    
    Constraint_22(i,k_h)      "Enthalpy variation of each hot process stream in Plant 4 at each stage"
    
    Constraint_24c            "Logical constraint to determine the existence of an ORC evaporator, thereby confirming the presence of an ORC"
    
    Constraint_25(i,k_h)      "Logical constraint to determine the existence of a heat exchanger at each stage in Plant 4 between hot process stream and cold intermediate fluid"
    
    Constraint_28(i,k_h)      "Maximum approach temperature at the left side of the HEX between each hot process stream in Plant 4 and the cold intermediate fluid at each stage"
    
    Constraint_29(i,k_h)      "Maximum approach temperature at the right side of the HEX between each hot process stream in Plant 4 and the cold intermediate fluid at each stage"
    
    Constraint_34(i)        "Cooling utility load for each hot process stream in Plant 4 at the last superstructure stage"
    
    
    Constraint_36(p,s,pp,ss)    "Internal diameter of the pipe transporting intermediate fluid"
    Constraint_39(p,s,pp,ss)    "Pipeline capital cost per unit length"
    Constraint_40(p,s,pp,ss)    "Capital cost of pipeline adjusted to May 2024 price"
    
    Constraint_41(p,s,pp,ss)    "Velocity of the intermediate fluid"
    Constraint_42(p,s,pp,ss)    "Reynolds number of the intermediate fluid"
    Constraint_43(p,s,pp,ss)    "Friction factor of the intermediate fluid"
    
    Constraint_44(p,s,pp,ss)    "Pressure drop of IHTF along the pipeline"
    
    Constraint_45(p,s,pp,ss)    "Capital cost of IHTF pump"
    
    Constraint_46(p,s,pp,ss)    "IHTF pump power consumption"
    
    Constraint_47(p,s,pp,ss)    "Annualized pump operation cost for transporting intermediate fluid stream"
    Constraint_48(p,s,pp,ss)    "Total pumping cost for transporting intermediate fluid stream"
    
    Constraint_49(i,k_h)      "Calculation of HEX area between cold intermediate fluid and hot process stream in Plant 4"
    
    Constraint_49a(i,k_h)       "Calculation of capital cost of HEX between hot process stream and cold IHTF in Plant 4"
    
    Constraint_51a              "Equating condenser temperature to the ORC working fluid temperature at pump inlet of ORC system 1 (state 1)"
    Constraint_52a              "Equating condenser temperature to the ORC working fluid temperature at the intermediate point of the condenser of ORC system 1 (state 6)"
    Constraint_53a              "Equating evaporator temperature to the ORC working fluid temperature at the intermediate point of the evaporator of ORC system 1 (state 3)"
    Constraint_54a              "Equating evaporator temperature to the ORC working fluid temperature at the turbine inlet of ORC system 1 (state 4)"
    Constraint_55               "Condenser pressure as a function of the condenser temperature"
    Constraint_56               "Evaporator pressure as a function of the evaporator temperature"
    Constraint_57a              "Enthalpy of the ORC working fluid at the pump inlet as a function of the condenser temperature of ORC system 1 (state 1)"
    Constraint_58a              "Entropy of the ORC working fluid at the pump inlet as a function of the condenser temperature of ORC system 1 (state 1)"
    Constraint_59a              "Requirement for isentropic condition at the ORC pump of ORC system 1"
    Constraint_60a              "Temperature of the ORC working fluid at the evaporator inlet as a function of the evaporator pressure and ORC working fluid entropy at the pump outlet of ORC system 1 (state 2)"
    Constraint_61a              "Enthalpy of the ORC working fluid at the evaporator inlet as a function of the evaporator pressure and ORC working fluid entropy at the pump outlet of ORC system 1 (state 2)"
    Constraint_62a              "Enthalpy of the ORC working fluid at the intermediate point of the evaporator as a function of the evaporator temperature of ORC system 1 (state 3)"
    Constraint_63a              "Enthalpy of the ORC working fluid at the turbine inlet as a function of the evaporator temperature of ORC system 1 (state 4)"
    Constraint_64a              "Entropy of the ORC working fluid at the turbine inlet as a function of the evaporator temperature of ORC system 1 (state 4)"
    Constraint_65a              "Requirement for isentropic condition at the ORC turbine of ORC system 1"
    Constraint_66a              "Temperature of the ORC working fluid at the turbine outlet as a function of the condenser temperature and ORC working fluid entropy at the turbine outlet of ORC system 1 (state 5)"
    Constraint_67a              "Enthalpy of the ORC working fluid at the turbine outlet as a function of the condenser pressure and ORC working fluid entropy at the turbine outlet of ORC system 1 (state 5)"
    Constraint_68a              "Enthalpy of the ORC working fluid at the intermediate point of the condenser as a function of the condenser temperature of ORC system 1 (state 6)"
    Constraint_69a              "Dividing the evaporator heat load between the sensible and latent heat loads transferred to the ORC working fluid of ORC system 1"
    Constraint_70a              "Sensible heat transferred to the ORC working fluid at the subcooled region in ORC system 1"
    Constraint_71a              "Latent heat transferred to the ORC working fluid at the wet region in ORC system 1"
    Constraint_72a              "ORC turbine power production in ORC system 1"
    Constraint_73a              "ORC pump power consumption in ORC system 1"
    Constraint_74a              "ORC condenser heat rejection rate in ORC system 1"
    Constraint_75a              "Dividing the condenser heat rejection rate between the sensible and latent heat transfers from the ORC working fluid in ORC system 1"
    Constraint_76a              "Sensible heat rejected by the ORC working fluid at the superheated region in ORC system 1"
    Constraint_77a              "Latent heat rejected by the ORC working fluid at the wet region in ORC system 1"
    Constraint_78a              "Approach temperature at the evaporator inlet of ORC system 1"
    Constraint_79a              "Approach temperature at the intermediate point of the evaporator of ORC system 1"
    Constraint_80a              "Approach temperature at the evaporator outlet of ORC system 1"
    Constraint_81a              "Constraint to ensure that the intermediate fluid temperature at the intermediate point of the evaporator exceeds the ORC evaporator temperature by at least the EMAT of the ORC in ORC system 1"
    Constraint_82a              "Approach temperature at the condenser inlet of ORC system 1"
    Constraint_83a              "Approach temperature at the intermediate point of the condenser of ORC system 1"
    Constraint_84a              "Approach temperature at the condenser outlet of ORC system 1"
    Constraint_85a              "Constraint to ensure that the ORC working fluid temperature at the intermediate point of the condenser exceeds the condenser cooling water temperature by at least the EMAT of the ORC in ORC system 1"
    Constraint_86a              "Evaporator area between the inlet and the intermediate point of ORC system 1"
    Constraint_87a              "Evaporator area between the intermediate point and the outlet of ORC system 1"
    Constraint_88a              "Condenser area between the inlet and the intermediate point of ORC system 1"
    Constraint_89a              "Condenser area between the intermediate point and the outlet of ORC system 1"
    Constraint_90a              "Capital cost of the evaporator area between the inlet and the intermediate point of ORC system 1"
    Constraint_91a              "Capital cost of the evaporator area between the intermediate point and the outlet of ORC system 1"
    Constraint_92a              "Capital cost of the condenser area between the inlet and the intermediate point of ORC system 1"
    Constraint_93a              "Capital cost of the condenser area between the intermediate point and the outlet of ORC system 1"
    Constraint_94a              "Capital cost of the ORC pump of ORC system 1"
    Constraint_95a              "Capital cost of the ORC turbine of ORC system 1"
    Constraint_96a              "Annual revenue from the sale of produced electricity from ORC system 1"
    Constraint_97a              "Annual operational cost of the ORC pump of ORC system 1"
    Constraint_98a              "Heat absorbed by the condenser cooling water as it passes through the outlet to the intermediate point of the condenser of ORC system 1"
    Constraint_99a              "Heat absorbed by the condenser cooling water as it passes through the intermediate point to the inlet of the condenser of ORC system 1"
    Constraint_100a             "Heat rejected by the hot intermediate fluid to the ORC working fluid which corresponds to the sensible heat received by the ORC working fluid in ORC system 1"
    Constraint_101a             "Heat rejected by the hot intermediate fluid to the ORC working fluid which corresponds to the latent heat received by the ORC working fluid in ORC system 1"
    
    Objective               "Minimize total annualized cost"    ;
    
Constraint_1c.. Mh('ORC') =e= m('ORC','hot_IF','P4','cold_IF') ;
Constraint_1d.. Mh('ORC') =e= m_ORC_evap_1 + m_ORC_bypass ;
Constraint_2.. Mc('P4') =e= m('P4','cold_IF','ORC','hot_IF') ;

Constraint_3a.. y('P4','cold_IF','ORC','hot_IF') =e= m('P4','cold_IF','ORC','hot_IF') / (m('P4','cold_IF','ORC','hot_IF') + delta) ;
Constraint_3d.. y('ORC','hot_IF','P4','cold_IF') =e= m('ORC','hot_IF','P4','cold_IF') / (m('ORC','hot_IF','P4','cold_IF') + delta) ;

Constraint_4c.. Mh('ORC') =e= m('P4','cold_IF','ORC','hot_IF') ;
Constraint_5.. Mc('P4') =e= m('ORC','hot_IF','P4','cold_IF') ;

Constraint_6a.. m('P4','cold_IF','ORC','hot_IF') * (tcmout('P4') - thmin('ORC')) * 10**6 =e= pi * Din('P4','cold_IF','ORC','hot_IF') * L('P4','ORC') * U * delta_T ;
Constraint_7a.. m('ORC','hot_IF','P4','cold_IF') * thmout('ORC') =e= m_ORC_evap_1 *  thmout_ORC_evap_1 + m_ORC_bypass * thmin('ORC') ;
Constraint_7b.. m('ORC','hot_IF','P4','cold_IF') * (thmout('ORC') - tcmin('P4')) * 10**6 =e= pi * Din('ORC','hot_IF','P4','cold_IF') * L('ORC','P4') * U * delta_T ;

Constraint_9.. tcm('P4',firstKh) =e= tcmout('P4') ;
Constraint_10(i).. t_h('P4',i,firstKh) =e= tin_h('P4',i) ;
Constraint_12.. tcm('P4',lastKh) =e= tcmin('P4') ;
Constraint_14(i).. F_h('P4',i) * (tin_h('P4',i) - tout_h('P4',i)) =e= sum(sth, qc('P4',i,sth)) + qu_h('P4',i) ;
Constraint_16(i,k_h)$sth(k_h).. t_h('P4',i,k_h) =g= t_h('P4',i,k_h+1) ;
Constraint_19(k_h)$sth(k_h).. tcm('P4',k_h) =g= tcm('P4',k_h+1) ;
Constraint_19a.. thmin('ORC') =g= thminter_ORC_evap_1 ;
Constraint_19b.. thminter_ORC_evap_1 =g= thmout_ORC_evap_1 ;
Constraint_19c.. tinter_ORC_cond_coolant_1 =g= TIN_ORC_cond_coolant_1 ;
Constraint_19d.. tout_ORC_cond_coolant_1 =g= tinter_ORC_cond_coolant_1 ;

Constraint_20c.. m_ORC_evap_1 * (thmin('ORC') - thmout_ORC_evap_1) =e= q_ORC_evap_1 ;
Constraint_21(k_h)$sth(k_h).. Mc('P4') * (tcm('P4',k_h) - tcm('P4',k_h+1)) =e= sum(i,qc('P4',i,k_h)) ;

Constraint_22(i,k_h)$sth(k_h).. F_h('P4',i) * (t_h('P4',i,k_h) - t_h('P4',i,k_h+1)) =e= qc('P4',i,k_h) ;
Constraint_24c.. zorc_1 =e= q_ORC_evap_1 / (q_ORC_evap_1 + delta) ;
Constraint_25(i,k_h)$sth(k_h).. zc('P4',i,k_h) =e= qc('P4',i,k_h) / (qc('P4',i,k_h) + delta) ;


Constraint_28(i,k_h)$sth(k_h).. dtc('P4',i,k_h) =l= t_h('P4',i,k_h) - tcm('P4',k_h) + (1 - zc('P4',i,k_h)) * gamma_c('P4',i) ;
Constraint_29(i,k_h)$sth(k_h).. dtc('P4',i,k_h+1) =l= t_h('P4',i,k_h+1) - tcm('P4',k_h+1) + (1 - zc('P4',i,k_h)) * gamma_c('P4',i) ;
Constraint_34(i).. qu_h('P4',i) =e= F_h('P4',i) * (t_h('P4',i,lastKh) - tout_h('P4',i)) ;

Constraint_36(p,s,pp,ss)$plant_IF(p,s,pp,ss).. Din(p,s,pp,ss) =e= (6.05*10**(-4) * Hy * C_power * (m(p,s,pp,ss) * 1000 / cp)**2.84 * mu**0.16 * rho**(-2) / (etta * n_pipe * B_pipe * (1 + F) * (Af + b_maint)))**(1/(4.84+n_pipe)) ;

Constraint_39(p,s,pp,ss)$plant_IF(p,s,pp,ss).. Pl(p,s,pp,ss) =e= 4.7 * Din(p,s,pp,ss)**1.7 ;

Constraint_40(p,s,pp,ss)$(plant_IF(p,s,pp,ss) and plant_connect(p,pp)).. Piping(p,s,pp,ss) =e= Af * L(p,pp) * Pl(p,s,pp,ss) * CEPCI_2024_pipe / CEPCI_2006_pipe ;

Constraint_41(p,s,pp,ss)$plant_IF(p,s,pp,ss).. v(p,s,pp,ss) * cp * pi * rho * Din(p,s,pp,ss) * Din(p,s,pp,ss) =e= 4 * m(p,s,pp,ss) * 1000 ;
Constraint_42(p,s,pp,ss)$plant_IF(p,s,pp,ss).. Re(p,s,pp,ss) * mu * 1000 =e= rho * Din(p,s,pp,ss) * v(p,s,pp,ss) ;
Constraint_43(p,s,pp,ss)$plant_IF(p,s,pp,ss).. fr(p,s,pp,ss) * (Re(p,s,pp,ss) * 1000)**0.2 =e= 0.046 ;

Constraint_44(p,s,pp,ss)$(plant_IF(p,s,pp,ss) and plant_connect(p,pp)).. delta_P(p,s,pp,ss) * Din(p,s,pp,ss) * 1000 =e= 2 * fr(p,s,pp,ss) * L(p,pp) * rho * v(p,s,pp,ss) * v(p,s,pp,ss) ;
    
Constraint_45(p,s,pp,ss)$plant_IF(p,s,pp,ss).. log10(Pumpcapital(p,s,pp,ss) * 1000 + delta) =e= y(p,s,pp,ss)*K1_pump + K2_pump*log10(Q(p,s,pp,ss)+delta) + K3_pump*log10(Q(p,s,pp,ss)+delta)*log10(Q(p,s,pp,ss)+delta) ;

Constraint_46(p,s,pp,ss)$plant_IF(p,s,pp,ss).. Q(p,s,pp,ss) * cp * rho * etta =e= m(p,s,pp,ss) * delta_P(p,s,pp,ss) * 1000 ;

Constraint_47(p,s,pp,ss)$plant_IF(p,s,pp,ss).. Pumpoperation(p,s,pp,ss) * 1000 =e= C_power * Hy * Q(p,s,pp,ss);

Constraint_48(p,s,pp,ss)$plant_IF(p,s,pp,ss).. Pumping(p,s,pp,ss) =e= Pumpoperation(p,s,pp,ss) + Af * Pumpcapital(p,s,pp,ss) * CEPCI_2024_plant / CEPCI_2001_plant ;

Constraint_49(i,k_h).. qc('P4',i,k_h)*1000*(ho**(-1)+h_h('P4',i)**(-1)) =e= A_h('P4',i,k_h)*((dtc('P4',i,k_h)*dtc('P4',i,k_h+1)*0.5*(dtc('P4',i,k_h)+dtc('P4',i,k_h+1)))**0.33333) ;

Constraint_49a(i,k_h)$sth(k_h).. log10(HEX_h_cost('P4',i,k_h)*1000+delta) =e= zc('P4',i,k_h)*K1_HEX + K2_HEX*log10(A_h('P4',i,k_h)+delta) + K3_HEX*log10(A_h('P4',i,k_h)+delta)*log10(A_h('P4',i,k_h)+delta) ;

Constraint_51a.. Tcond =e= T1_1 ;
Constraint_52a.. Tcond =e= T6_1 ;
Constraint_53a.. Tevap =e= T3_1 ;
Constraint_54a.. Tevap =e= T4_1 ;
Constraint_55.. Pcond =e= 0.0106 * Tcond - 0.1797 ;
Constraint_56.. Pevap =e= 0.0287 * Tevap - 1.5297 ;
Constraint_57a.. h1_1 =e= 1.385 * Tcond + 196.8 ;
Constraint_58a.. s1_1 =e= 0.0043 * Tcond + 1.0077 ;
Constraint_59a.. s2_1 =e= s1_1 ;
Constraint_60a.. T2_1 =e= -236.6779 + 0.4335 * Pevap + 234.7105 * s2_1 ;
Constraint_61a.. h2_1 =e= -129.1998 + 0.7871 * Pevap + 323.5453 * s2_1 ;
Constraint_62a.. h3_1 =e= 1.5929 * Tevap + 181.23 ;
Constraint_63a.. h4_1 =e= -0.0036 * Tevap * Tevap + 1.2645 * Tevap + 384.94 ;
Constraint_64a.. s4_1 =e= -0.000006 * Tevap * Tevap + 0.0016 * Tevap + 1.6934 ;
Constraint_65a.. s5_1 =e= s4_1 ;
Constraint_66a.. T5_1 =e= -565.2355 + 76.9486 * Pcond + 333.2629 * s5_1 ;
Constraint_67a.. h5_1 =e= -161.4453 + 53.9855 * Pcond + 331.0438 * s5_1 ;
Constraint_68a.. h6_1 =e= 0.7256 * Tcond + 405.62 ;
Constraint_69a.. q_ORC_evap_1 =e= q_ORC_evap_subcooled_1 + q_ORC_evap_wet_1 ;
Constraint_70a.. q_ORC_evap_subcooled_1 * 1000 =e= m_ORC_WF_1 * (h3_1 - h2_1) ;
Constraint_71a.. q_ORC_evap_wet_1 * 1000 =e= m_ORC_WF_1 * (h4_1 - h3_1) ;
Constraint_72a.. E_ORC_turb_1 * 1000 =e= m_ORC_WF_1 * (h4_1 - h5_1) ;
Constraint_73a.. E_ORC_pump_1 * 1000 =e= m_ORC_WF_1 * (h2_1 - h1_1) ;
Constraint_74a.. q_ORC_cond_1 =e= q_ORC_evap_1 + E_ORC_pump_1 - E_ORC_turb_1 ;
Constraint_75a.. q_ORC_cond_1 =e= q_ORC_cond_superheat_1 + q_ORC_cond_wet_1 ;
Constraint_76a.. q_ORC_cond_superheat_1 * 1000 =e= m_ORC_WF_1 * (h5_1 - h6_1) ;
Constraint_77a.. q_ORC_cond_wet_1 * 1000 =e= m_ORC_WF_1 * (h6_1 - h1_1) ;
Constraint_78a.. dt_in_ORC_evap_1 =l= thmout_ORC_evap_1 - T2_1 + (1 - zorc_1) * gamma_h_ORC_evap ;
Constraint_79a.. dt_inter_ORC_evap_1 =l= thminter_ORC_evap_1 - T3_1 + (1 - zorc_1) * gamma_h_ORC_evap ;
Constraint_80a.. dt_out_ORC_evap_1 =l= thmin('ORC') - T4_1 + (1 - zorc_1) * gamma_h_ORC_evap ;
Constraint_81a.. thminter_ORC_evap_1 - T3_1 =g= EMAT_ORC ;
Constraint_82a.. dt_in_ORC_cond_1 =l= T5_1 - tout_ORC_cond_coolant_1 + (1 - zorc_1) * gamma_coolant_ORC_cond ;
Constraint_83a.. dt_inter_ORC_cond_1 =l= T6_1 - tinter_ORC_cond_coolant_1 + (1 - zorc_1) * gamma_coolant_ORC_cond ;
Constraint_84a.. dt_out_ORC_cond_1 =l= T1_1 - TIN_ORC_cond_coolant_1 + (1 - zorc_1) * gamma_coolant_ORC_cond ;
Constraint_85a.. T6_1 - tinter_ORC_cond_coolant_1 =g= EMAT_ORC ;
Constraint_86a.. q_ORC_evap_subcooled_1 * 1000 * (ho**(-1)+h_ORC**(-1)) =e= A_ORC_evap_subcooled_1*(dt_in_ORC_evap_1*dt_inter_ORC_evap_1*0.5*(dt_in_ORC_evap_1+dt_inter_ORC_evap_1))**0.33333 ;
Constraint_87a.. q_ORC_evap_wet_1 * 1000 * (ho**(-1)+h_ORC**(-1)) =e= A_ORC_evap_wet_1*(dt_inter_ORC_evap_1*dt_out_ORC_evap_1*0.5*(dt_inter_ORC_evap_1+dt_out_ORC_evap_1))**0.33333 ;
Constraint_88a.. q_ORC_cond_superheat_1 * 1000 * (h_ORC_cond_coolant**(-1)+h_ORC**(-1)) =e= A_ORC_cond_superheat_1*(dt_inter_ORC_cond_1*dt_in_ORC_cond_1*0.5*(dt_inter_ORC_cond_1+dt_in_ORC_cond_1))**0.33333 ;
Constraint_89a.. q_ORC_cond_wet_1 * 1000 * (h_ORC_cond_coolant**(-1)+h_ORC**(-1)) =e= A_ORC_cond_wet_1*(dt_inter_ORC_cond_1*dt_out_ORC_cond_1*0.5*(dt_inter_ORC_cond_1+dt_out_ORC_cond_1))**0.33333 ;
Constraint_90a.. log10(ORC_evap_subcooled_cost_1*1000+delta) =e= zorc_1*K1_HEX+K2_HEX*log10(A_ORC_evap_subcooled_1+delta)+K3_HEX*log10(A_ORC_evap_subcooled_1+delta)*log10(A_ORC_evap_subcooled_1+delta) ;
Constraint_91a.. log10(ORC_evap_wet_cost_1*1000+delta) =e= zorc_1*K1_HEX+K2_HEX*log10(A_ORC_evap_wet_1+delta)+K3_HEX*log10(A_ORC_evap_wet_1+delta)*log10(A_ORC_evap_wet_1+delta) ;
Constraint_92a.. log10(ORC_cond_superheat_cost_1*1000+delta) =e= zorc_1*K1_HEX+K2_HEX*log10(A_ORC_cond_superheat_1+delta)+K3_HEX*log10(A_ORC_cond_superheat_1+delta)*log10(A_ORC_cond_superheat_1+delta) ;
Constraint_93a.. log10(ORC_cond_wet_cost_1*1000+delta) =e= zorc_1*K1_HEX+K2_HEX*log10(A_ORC_cond_wet_1+delta)+K3_HEX*log10(A_ORC_cond_wet_1+delta)*log10(A_ORC_cond_wet_1+delta) ;
Constraint_94a.. log10(Cost_ORC_pump_1*1000+delta) =e= zorc_1*K1_pump+K2_pump*log10(E_ORC_pump_1*1000/etta+delta)+K3_pump*log10(E_ORC_pump_1*1000/etta+delta)*log10(E_ORC_pump_1*1000/etta+delta) ;
Constraint_95a.. log10(Cost_ORC_turb_1*1000+delta) =e= zorc_1*K1_turb+K2_turb*log10(E_ORC_turb_1*1000+delta)+K3_turb*log10(E_ORC_turb_1*1000+delta)*log10(E_ORC_turb_1*1000+delta) ;
Constraint_96a.. Revenue_ORC_1 * 1000 =e= Hy * Pe * E_ORC_turb_1 * 1000 ;
Constraint_97a.. Cost_ORC_pump_power_cons_1 * 1000 =e= Hy * C_power * E_ORC_pump_1 * 1000 / etta ;
Constraint_98a.. q_ORC_cond_wet_1 * 1000 =e= m_ORC_cond_coolant_1 * cp_cond_coolant * (tinter_ORC_cond_coolant_1 - TIN_ORC_cond_coolant_1) ;
Constraint_99a.. q_ORC_cond_superheat_1 * 1000 =e= m_ORC_cond_coolant_1 * cp_cond_coolant * (tout_ORC_cond_coolant_1 - tinter_ORC_cond_coolant_1) ;
Constraint_100a.. q_ORC_evap_subcooled_1 =e= m_ORC_evap_1 * (thminter_ORC_evap_1 - thmout_ORC_evap_1) ;
Constraint_101a.. q_ORC_evap_wet_1 =e= m_ORC_evap_1 * (thmin('ORC') - thminter_ORC_evap_1) ;

Objective.. TAC =e= CU_cow*sum(i,qu_h('P4',i))*1000 + CU_orc_cond*q_ORC_cond_1*1000
                  + sum((p,s,pp,ss)$plant_IF(p,s,pp,ss),Piping(p,s,pp,ss))
                  + sum((p,s,pp,ss)$plant_IF(p,s,pp,ss),Pumping(p,s,pp,ss))
                  + Af * sum((i,k_h),HEX_h_cost('P4',i,k_h)) * CEPCI_2024_plant / CEPCI_2001_plant
                  + Af * ORC_evap_subcooled_cost_1 * CEPCI_2024_plant / CEPCI_2001_plant
                  + Af * ORC_evap_wet_cost_1 * CEPCI_2024_plant / CEPCI_2001_plant
                  + Af * ORC_cond_superheat_cost_1 * CEPCI_2024_plant / CEPCI_2001_plant
                  + Af * ORC_cond_wet_cost_1 * CEPCI_2024_plant / CEPCI_2001_plant
                  + Af * Cost_ORC_pump_1 * CEPCI_2024_plant / CEPCI_2001_plant
                  + Af * Cost_ORC_turb_1 * CEPCI_2024_plant / CEPCI_2001_plant
                  + Cost_ORC_pump_power_cons_1 - Revenue_ORC_1 ; 
                  
*Variable bounds
dtc.lo('P4',i,k_h) = 10 ;
dtc.up('P4',i,k_h) = 99 ;
Din.up(p,s,pp,ss) = 0.65 ;
m.up(p,s,pp,ss) = 1 ;
Mc.up(p) = 1 ;
Mh.up(p) = 1 ;
qc.up('P4',i,k_h) = 9.018 ;
Q.up(p,s,pp,ss) = 300 ;
A_h.up('P4',i,k_h) = 1000 ;
Pumpcapital.up(p,s,pp,ss) = 30 ;
HEX_h_cost.up('P4',i,k_h) = 77 ;

Tcond.lo = 40 ;
Tcond.up = 60 ;
Tevap.lo = 70 ;
Tevap.up = 130 ;
tout_ORC_cond_coolant_1.up = 35 ;
thmout_ORC_evap_1.up = 70 ;
dt_in_ORC_evap_1.lo = 10 ;
dt_inter_ORC_evap_1.lo = 10 ;
dt_out_ORC_evap_1.lo = 10 ;
dt_in_ORC_evap_1.up = 99 ;
dt_inter_ORC_evap_1.up = 99 ;
dt_out_ORC_evap_1.up = 99 ;
dt_in_ORC_cond_1.lo = 10 ;
dt_inter_ORC_cond_1.lo = 10 ;
dt_out_ORC_cond_1.lo = 10 ;
dt_in_ORC_cond_1.up = 99 ;
dt_inter_ORC_cond_1.up = 99 ;
dt_out_ORC_cond_1.up = 99 ;
A_ORC_evap_subcooled_1.up = 1000 ;
A_ORC_evap_wet_1.up = 1000 ;
A_ORC_cond_superheat_1.up = 1000 ;
A_ORC_cond_wet_1.up = 1000 ;
E_ORC_pump_1.up = 0.3 ;
E_ORC_turb_1.up = 4 ;
m_ORC_WF_1.up = 84 ;
h1_1.lo = 252 ;
h1_1.up = 280 ;
h2_1.lo = 252 ;
h2_1.up = 282 ;
h3_1.lo = 294 ;
h3_1.up = 391 ;
h4_1.lo = 456 ;
h4_1.up = 488 ;
h5_1.lo = 439 ;
h5_1.up = 459 ;
h6_1.lo = 434 ;
h6_1.up = 450 ;
m_ORC_cond_coolant_1.up = 693.33 ;

Model replica_model / all /;

Option OptCR = 0.2;
Option ResLim = 28000 ;

Solve replica_model using NLP minimizing TAC;
